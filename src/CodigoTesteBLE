#include <Arduino.h>
#include <BLEDevice.h>
#include <BLEUtils.h>
#include <BLEServer.h>

// UUIDs
const char* CIA_CONTROL_ENABLE = "22086d8b-57c2-4eb4-b82d-4b7936413e78";
const char* CONTROLE_TEMP      = "2f7d55d9-acce-4a1d-9871-dd3e4ec73eec";
const char* ENABLE_1           = "1b4a81b4-abf5-450d-9d4d-81b4e951baa7";
const char* ENABLE_2           = "d2b757c1-9fb1-4166-af83-467a0a0a55f3";
const char* ENABLE_3           = "765170c6-24e5-4890-a346-10195b57ca7c";
const char* AlTemp_1           = "28b66ce2-882e-46e2-bc17-1332f33072db";
const char* AlTemp_2           = "83ec522b-14b0-4b42-8a8b-0924993f5490";
const char* AlTemp_3           = "e49b8658-6f76-43dd-a36e-db7f4b1aa546";

const char* SERVICE_UUID[2]        = { CIA_CONTROL_ENABLE, CONTROLE_TEMP };
const char* CHARACTERISTIC_UUID[6] = { ENABLE_1, ENABLE_2, ENABLE_3, AlTemp_1, AlTemp_2, AlTemp_3 };

// variáveis simuladas
bool controleAtivo[3] = { false, false, false };
int setpoint[3]       = { 25, 25, 25 };
float temperatura[3]  = { 22.0, 23.0, 24.0 };

// controle da simulação
unsigned long ultimoUpdate = 0;
bool sobe[3] = { true, true, true };

// ---------------------- Helpers ----------------------
static void printBytes(const std::string &value, const char* tag = nullptr) {
  if (tag) Serial.print(tag);
  Serial.printf(" [len=%u] bytes:", (unsigned)value.size());
  for (size_t i = 0; i < value.size(); ++i) {
    uint8_t b = static_cast<uint8_t>(value[i]);
    Serial.printf(" 0x%02X(%u)", b, b);
  }
  Serial.println();
}

// ---------------------- Classes BLE ----------------------
class enableServer : public BLEServerCallbacks {
  void onConnect(BLEServer* servidorCIA) override {
    Serial.println("[BLE] Dispositivo conectado!");
  }
  void onDisconnect(BLEServer* servidorCIA) override {
    Serial.println("[BLE] Dispositivo desconectado!");
  }
};

class enableControl : public BLECharacteristicCallbacks {
  bool &controle;
public:
  enableControl(bool &control) : controle(control) {}
  void onWrite(BLECharacteristic* ENABLE_CONTROL) override {
    const std::string value = ENABLE_CONTROL->getValue();

    if (!value.empty()) {
      printBytes(value, "[BLE] enableControl recebido:");

      uint8_t b0 = static_cast<uint8_t>(value[0]);
      if ((b0 == '0') || (b0 == '1')) controle = (b0 == '1');
      else controle = (b0 != 0);

      Serial.printf("[BLE] enableControl - primeiro byte: 0x%02X (%u) -> controleAtivo=%s\n",
                    b0, b0, controle ? "ON" : "OFF");
    } else {
      Serial.println("[BLE] enableControl onWrite - valor vazio recebido");
    }
  }
};

class functionsControl : public BLECharacteristicCallbacks {
  int &set_point;
  int sensorIndex;
public:
  functionsControl(int &st, int idx) : set_point(st), sensorIndex(idx) {}

  void onWrite(BLECharacteristic* variaveisBLE) override {
    const std::string value = variaveisBLE->getValue();
    if (!value.empty()) {
      printBytes(value, "[BLE] functionsControl recebido:");

      bool allAsciiDigits = true;
      for (char c : value) {
        if (!(isdigit((unsigned char)c) || c == '-' || c == '+')) { allAsciiDigits = false; break; }
      }

      if (allAsciiDigits) {
        int parsed = atoi(value.c_str());
        set_point = parsed;
        Serial.printf("[BLE] functionsControl - ASCII parsed: %d\n", parsed);
      } else {
        uint8_t b = static_cast<uint8_t>(value[0]);
        set_point = b;
        Serial.printf("[BLE] functionsControl - byte value: %u\n", b);
      }

      Serial.printf("[BLE] Novo setpoint[%d]: %d\n", sensorIndex, set_point);
    } else {
      Serial.println("[BLE] functionsControl onWrite - valor vazio recebido");
    }
  }

  void onRead(BLECharacteristic* variaveisBLE) override {
    std::string out = std::to_string(temperatura[sensorIndex]);
    variaveisBLE->setValue(out);
    Serial.printf("[BLE] functionsControl onRead - enviando temperatura sensor %d: %s\n",
                  sensorIndex, out.c_str());
  }
};

// ---------------------- Setup ----------------------
void setup() {
  Serial.begin(115200);
  Serial.println("\n=== Iniciando ESP32S_CIA - Teste BLE ===");

  BLEDevice::init("ESP32S_CIA");
  BLEServer *servidorCIA = BLEDevice::createServer();
  servidorCIA->setCallbacks(new enableServer());

  BLEService *controlTemp[2];
  for (int i = 0; i < 2; i++) {
    controlTemp[i] = servidorCIA->createService(SERVICE_UUID[i]);
    Serial.printf("[BLE] Serviço criado UUID: %s\n", SERVICE_UUID[i]);
  }

  BLECharacteristic *variaveisBLE[6];
  int k = 0, l = 0;
  for (int i = 0; i < 2; i++) {
    for (int j = 0; j < 3; j++) {
      variaveisBLE[k] = controlTemp[i]->createCharacteristic(
        CHARACTERISTIC_UUID[l],
        BLECharacteristic::PROPERTY_READ   |
        BLECharacteristic::PROPERTY_WRITE  |
        BLECharacteristic::PROPERTY_NOTIFY |
        BLECharacteristic::PROPERTY_INDICATE
      );
      Serial.printf("[BLE] Característica criada UUID: %s\n", CHARACTERISTIC_UUID[l]);
      k++; l++;
    }
  }

  for (int i = 0; i < 2; i++) controlTemp[i]->start();

  for (int i = 0; i < 3; i++) variaveisBLE[i]->setCallbacks(new enableControl(controleAtivo[i]));
  for (int i = 3; i < 6; i++) variaveisBLE[i]->setCallbacks(new functionsControl(setpoint[i - 3], i - 3));

  BLEAdvertising *pAdvertising = BLEDevice::getAdvertising();
  pAdvertising->setScanResponse(true);
  pAdvertising->start();
  Serial.println("[BLE] Advertising iniciado. Aguardando conexão...");
}

// ---------------------- Loop ----------------------
void loop() {
  unsigned long agora = millis();
  if (agora - ultimoUpdate >= 1000) { // cada 1s
    ultimoUpdate = agora;
    for (int i = 0; i < 3; i++) {
      if (sobe[i]) {
        temperatura[i] += 0.5;
        if (temperatura[i] >= 30.0) sobe[i] = false;
      } else {
        temperatura[i] -= 0.5;
        if (temperatura[i] <= 20.0) sobe[i] = true;
      }
      Serial.printf("[SIM] Temperatura[%d]=%.1f\n", i, temperatura[i]);
    }
  }
}
